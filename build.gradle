plugins {
    id "architectury-plugin" version "3.0-SNAPSHOT"
    id "dev.architectury.loom" version "0.7.2-SNAPSHOT" apply false
    id "org.sonarqube" version "3.1.1"
    id "com.matthewprenger.cursegradle" version "1.1.0"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.16.5+build.4"

        compileOnly 'org.jetbrains:annotations:20.1.0'
        annotationProcessor 'org.spongepowered:mixin:0.8:processor'
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven { url "https://nexus.resourcefulbees.com/repository/maven-public/" }
        maven {
            url "https://www.cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"

        // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
        // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
        // We'll use that if it's available, but otherwise we'll use the older option.
        def targetVersion = 8
        if (JavaVersion.current().isJava9Compatible()) {
            options.release = targetVersion
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
        withSourcesJar()
    }

    sonarqube {
        properties {
            property "sonar.projectKey", "Resourceful-Bees_ResourcefulBees"
            property "sonar.organization", "resourceful-bees"
            property "sonar.host.url", "https://sonarcloud.io"
        }
    }
}
