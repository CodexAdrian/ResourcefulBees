plugins {
    id "architectury-plugin" version "3.4+"
    id "dev.architectury.loom" version "1.1.+" apply false
    id "org.sonarqube" version "4.0.+"
    id "com.teamresourceful.resourcefulgradle"
}

architectury {
    minecraft = mc_version
}

ext {
    repoUrl = nightly.toBoolean() ? "https://nexus.resourcefulbees.com/repository/maven-nightly/" : "https://nexus.resourcefulbees.com/repository/maven-releases/"
    ENV = System.getenv()
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:$mc_version"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-$mc_version:$parchment_version@zip")
        }
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:@YARN_MAPPINGS@:v2"
    }
}

sonar {
    properties {
        property "sonar.projectKey", "Resourceful-Bees_ResourcefulBees"
        property "sonar.organization", "resourceful-bees"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = archives_base_name
    version = libs.versions.mod.version.get() //resourcefulGradle.modVersion.getModVersion(project)
    group = maven_group

    repositories {
        maven { url = "https://nexus.resourcefulbees.com/repository/maven-public/" }
        mavenCentral()
        maven { url = "https://www.cursemaven.com" }
        mavenLocal()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}


resourcefulGradle {
    templates {
        //I have no idea how to get rid of the warnings in idea. switching from interface to abstract
        //gets rid of *some* warnings. so we just have to live with them as they are now since that's how
        //the gradle docs say to make a plugin with nested DSL

        readme {
            source = file("dev_resources/project_templates/README.md.template")
            injectedValues = [
                    "version"            : libs.versions.mod.version.get(),
                    "mc_version"         : mc_version,
                    "phos_version"       : phos_version,
                    "gecko_forge"        : geckolib_forge,
                    "gecko_forge_version": geckolib_version,
                    "rlib_version"       : rlib_version,
                    "rconfig_version"    : rconfig_version
            ]
        }
        discordEmbed {
            injectedValues = [
                    "version"      : libs.versions.mod.version.get(),
                    "mc_version"   : mc_version,
                    "forge_version": forge_version
            ]

            def template = nightly.toBoolean() ? "nightly_embed.json.template" : "release_embed.json.template"
            source = file("dev_resources/project_templates/$template")
        }
    }
}
