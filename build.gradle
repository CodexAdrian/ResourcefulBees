import com.teamresourceful.ModVersionPlugin
import com.teamresourceful.tasks.CopyTemplateTask

plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.0.+" apply false
    id "org.sonarqube" version "3.5.+"
}

architectury {
    minecraft = rootProject.minecraft_version
}

ext {
    repoUrl = nightly.toBoolean() ? "https://nexus.resourcefulbees.com/repository/maven-nightly/" : "https://nexus.resourcefulbees.com/repository/maven-releases/"
    ENV = System.getenv()
}

apply plugin: ModVersionPlugin
modVersion {
    updateModVersion {
        initialMCVersion = initial_mc_version
        currentMCVersion = minecraft_version
        patchVersion = patch
        versionPostfix = postfix
        postfixBuild = postfix_build
        isNightly = nightly.toBoolean()
        buildTime = build_time
    }
}


subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_version}@zip")
        }
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:@YARN_MAPPINGS@:v2"
    }
}
println "${name}: ${modVersion.version}"

sonar {
    properties {
        property "sonar.projectKey", "Resourceful-Bees_ResourcefulBees"
        property "sonar.organization", "resourceful-bees"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = modVersion.version
    group = rootProject.maven_group

    repositories {
        maven { url = "https://nexus.resourcefulbees.com/repository/maven-public/" }
        mavenCentral()
        maven { url = "https://www.cursemaven.com" }
        mavenLocal()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

tasks.register('createDiscordEmbed', CopyTemplateTask) {
    versionProperties = [
            "version"      : version,
            "mc_version"   : minecraft_version,
            "forge_version": forge_version
    ]

    def template = nightly.toBoolean() ? "nightly_embed.json.template" : "release_embed.json.template"
    source = file("dev_resources/project_templates/$template")
    //targetDir = file(".")
}

tasks.register('updateReadme', CopyTemplateTask) {
    source = file("dev_resources/project_templates/README.md.template")
    //targetDir = file(".")
    versionProperties = [
            "version"            : version,
            "mc_version"         : minecraft_version,
            "phos_version"       : phos_version,
            "gecko_forge"        : geckolib_forge,
            "gecko_forge_version": geckolib_forge_version,
            "rlib_version"       : resourcefullib_version,
            "rconfig_version"    : resourcefulconfig_version
    ]
}
