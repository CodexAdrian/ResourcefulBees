buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://nexus.resourcefulbees.com/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.4.0'
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}
plugins {
    id "org.sonarqube" version "3.1.1"
    id "com.matthewprenger.cursegradle" version "1.4.0"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'signing'
apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'


repositories {
    maven { url "https://nexus.resourcefulbees.com/repository/maven-public/" }
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

version = "${mod_version}"
group = "com.resourcefulbees" // http://maven.apache.org/guides/mini/guide-naming-conventions.html

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
evaluationDependsOnChildren()

def releasesRepoUrl = "https://nexus.resourcefulbees.com/repository/maven-releases/"
def snapshotsRepoUrl = "https://nexus.resourcefulbees.com/repository/maven-snapshots/"
def ENV = System.getenv()

sourceSets {
    main {
        java.excludes = ['com/teamresourceful/resourcefulbees/datagen/**']
        resources {
            srcDirs += 'src/generated/resources'
            exclude '.cache'
        }
    }
    datagen {
        resources.srcDirs = main.resources.srcDirs
        resources.srcDirs.remove('src/generated/resources')
        java.srcDirs = main.java.srcDirs
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "Resourceful-Bees_ResourcefulBees"
        property "sonar.organization", "resourceful-bees"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

minecraft {
    mappings channel: "official", version: "1.18.1"//"${mapping}"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=resourcefulbees.mixins.json"
            arg "-mixin.config=phosphophyllite.mixins.json"

            property 'forge.logging.console.level', 'debug'
            //property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'fml.earlyprogresswindow','false'
            property 'mixin.debug.export', 'true'

            mods {
                resourcefulbees {
                    source sourceSets.main
                }
                Phosphophyllite {
                    source project(':Phosphophyllite').sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            arg "-mixin.config=resourcefulbees.mixins.json"
            arg "-mixin.config=phosphophyllite.mixins.json"

            property 'forge.logging.console.level', 'debug'
            //property 'mixin.env.disableRefMap', 'true'
            property 'fml.earlyprogresswindow','false'

            mods {
                resourcefulbees {
                    source sourceSets.main
                }
                Phosphophyllite {
                    source project(':Phosphophyllite').sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.console.level', 'debug'
            args "--mod", "resourcefulbees", "--all", "--output", "\"" + file("src/generated/resources").toString() + "\"", "--validate", "--existing", "\"" + sourceSets.main.resources.srcDirs[0] + "\""
            mods {
                resourcefulbees {
                    source sourceSets.datagen
                }
                Phosphophyllite {
                    source project(':Phosphophyllite').sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "${modid}.refmap.json"
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}" //21

    compileOnly fg.deobf("mezz.jei:jei-1.18.1:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.18.1:${jei_version}")

    //compileOnly fg.deobf ("mcjty.theoneprobe:TheOneProbe-1.16:${top_version}:api")
    //runtimeOnly fg.deobf ("mcjty.theoneprobe:TheOneProbe-1.16:${top_version}")

    //compileOnly fg.deobf ("team.chisel.ctm:CTM:${ctm_version}")
    //runtimeOnly fg.deobf ("team.chisel.ctm:CTM:${ctm_version}")

    //compileOnly fg.deobf ("vazkii.patchouli:Patchouli:${patchouli_version}:api")
    //runtimeOnly fg.deobf ("vazkii.patchouli:Patchouli:${patchouli_version}")

    ///runtimeOnly fg.deobf ("mekanism:Mekanism:${mekanism_version}")// core
    //runtimeOnly fg.deobf ("mekanism:Mekanism:${mekanism_version}:additions")// Mekanism: Additions
    //runtimeOnly fg.deobf ("mekanism:Mekanism:${mekanism_version}:generators")// Mekanism: Generators
    //runtimeOnly fg.deobf ("mekanism:Mekanism:${mekanism_version}:tools")// Mekanism: Tools

    //runtimeOnly fg.deobf ("curse.maven:create-328085:${create_version}")
    //runtimeOnly fg.deobf ("curse.maven:flywheel-486392:${flywheel_version}")

    implementation fg.deobf("software.bernie.geckolib:geckolib-1.18-forge:${geckolib_version}")

    implementation project(':Phosphophyllite')

    compileOnly 'org.jetbrains:annotations:22.0.0'
    annotationProcessor 'org.spongepowered:mixin:0.8.5-SNAPSHOT:processor'
}

jar {
    manifest {
        attributes([
                "Specification-Title": "resourcefulbees",
                "Specification-Vendor": "resourcefulbees",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${getArchiveVersion()}",
                "Implementation-Vendor" :"resourcefulbees",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "${modid}.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allJava
}

task javadocs(type: Javadoc) {
    classpath = sourceSets.main.compileClasspath
    source = sourceSets.main.java
    include "com/resourcefulbees/**"

    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
}

task javadocJar(type: Jar, dependsOn: javadocs) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'ResourcefulBees'
                description = 'A mod for minecraft to add own bees to minecraft through a powerful api.'
                url = 'https://github.com/Resourceful-Bees/ResourcefulBees'

                scm {
                    connection = 'git:https://github.com/Resourceful-Bees/ResourcefulBees.git'
                    developerConnection = 'git:https://github.com/Resourceful-Bees/ResourcefulBees.git'
                    url = 'https://github.com/Resourceful-Bees/ResourcefulBees/'
                }

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://raw.githubusercontent.com/Resourceful-Bees/ResourcefulBees/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'epic428'
                        name = 'Epic_Oreo'
                        roles = ['Developer']
                        email = 'admin@resourcefulbees.com'
                    }
                    developer {
                        id = 'thatgravyboat'
                        name = 'ThatGravyBoat'
                        roles = ['Developer']
                        email = 'admin@resourcefulbees.com'
                    }
                    developer {
                        id = 'dawn_felstar'
                        name = 'Dawn Felstar'
                        roles = ['Developer']
                        email = 'admin@resourcefulbees.com'
                    }
                    developer {
                        id = '19PixelsPlaced'
                        name = 'Ellah'
                        roles = ['Artist']
                        email = 'admin@resourcefulbees.com'
                    }
                    developer {
                        id = 'ruby_r1der'
                        name = 'Joosh'
                        roles = ['Artist']
                        email = 'admin@resourcefulbees.com'
                    }
                }
            }

        }
    }

    repositories {
        maven {
            url = rootProject.version.endsWith("-SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ENV.MAVEN_USER
                password ENV.MAVEN_PASS
            }
        }
    }
}

ext {
    versionProperties = [
            "version"     : mod_version,
            "fmlVersion"  : forge_version.substring(0, forge_version.indexOf('.')),
            "forgeVersion": forge_version,
            "mcVersion"   : mc_version,
            "phosVersion" : project.project(":Phosphophyllite").property("compatibility").toString(),
            "geckoVersion": geckolib_version
    ]
}

def mainReplaceResources = createReplaceResourcesTask("main", sourceSets.main)
def datagenReplaceResources = createReplaceResourcesTask("datagen", sourceSets.main)

def createReplaceResourcesTask(String output, SourceSet sourceset) {
    return tasks.register("${output}ReplaceResources", Copy) {
        it.outputs.upToDateWhen { false }

        def modsToml = copySpec {
            from(sourceset.resources) {
                include "META-INF/mods.toml"
                expand versionProperties
            }
        }

        it.with modsToml
        it.into "$buildDir/resources/${output}/"

        if (new File("$rootDir/out/production/ResourcefulBees.${output}/").exists()) {
            copy {
                with modsToml
                into "$rootDir/out/production/ResourcefulBees.${output}/"
            }
        }

        if (new File("$rootDir/bin/${output}/").exists()) {
            copy {
                with modsToml
                into "$rootDir/bin/${output}/"
            }
        }
    }
}

processResources { setupProcessResources(processResources, mainReplaceResources) }
processDatagenResources { setupProcessResources(processDatagenResources, datagenReplaceResources) }

def setupProcessResources(ProcessResources process, TaskProvider replaceResources) {
    process.duplicatesStrategy(DuplicatesStrategy.FAIL)
    process.exclude("META-INF/mods.toml")
    process.from("${projectDir}") { include 'logo.png' }
    process.configure { finalizedBy(replaceResources)}
}

classes.configure { dependsOn(mainReplaceResources) }
datagenClasses.configure { dependsOn(datagenReplaceResources) }

curseforge {
    apiKey = "${ENV.CURSE_API_KEY}"
    project {
        id = '384508'
        changelog = file('changelog.md')
        changelogType = 'markdown'
        releaseType = 'release'
        addGameVersion "${mc_version}"
        mainArtifact(jar) {
            displayName = "Resourceful Bees ${version}"
        }
    }
}